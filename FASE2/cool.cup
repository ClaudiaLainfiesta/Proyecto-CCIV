/*
 *  cool.cup
 *              Parser definition for the COOL language.
 *
 */

import java_cup.runtime.*;

/* Stuff enclosed in {: :} is copied verbatim to the class containing
all parser actions.  All the extra variables/functions you want to use
in the semantic actions should go here.  Don't remove or modify anything
that was there initially.  */

action code {: 

    int curr_lineno() {
	return ((CoolTokenLexer)parser.getScanner()).curr_lineno();
    }

    AbstractSymbol curr_filename() {
	return ((CoolTokenLexer)parser.getScanner()).curr_filename();
    }
:} 

/************************************************************************/
/*                DONT CHANGE ANYTHING IN THIS SECTION                  */

parser code {:
    int omerrs = 0;

    public void syntax_error(Symbol cur_token) {
        int lineno = action_obj.curr_lineno();
	String filename = action_obj.curr_filename().getString();
        System.err.print("\"" + filename + "\", line " + lineno + 
		         ": parse error at or near ");
        Utilities.printToken(cur_token);
	omerrs++;
	if (omerrs>50) {
	   System.err.println("More than 50 errors");
	   System.exit(1);
	}
    }

    public void unrecovered_syntax_error(Symbol cur_token) {
    }
:}

/* Declare the terminals; a few have types for associated lexemes.  The
token ERROR is never used in the parser; thus, it is a parse error when
the lexer returns it.  */

terminal CLASS, ELSE, FI, IF, IN, INHERITS, LET, LET_STMT, LOOP, POOL, THEN, WHILE;
terminal CASE, ESAC, OF, DARROW, NEW, ISVOID;
terminal ASSIGN, NOT, LE, ERROR;
terminal PLUS, DIV, MINUS, MULT, EQ, LT, DOT, NEG, COMMA, SEMI, COLON;
terminal LPAREN, RPAREN, AT, LBRACE, RBRACE;
terminal AbstractSymbol STR_CONST, INT_CONST;
terminal Boolean BOOL_CONST;
terminal AbstractSymbol TYPEID, OBJECTID;

/*  DON'T CHANGE ANYTHING ABOVE THIS LINE, OR YOUR PARSER WONT WORK       */
/**************************************************************************/

   /* Complete the nonterminal list below, giving a type for the semantic
      value of each non terminal. (See the CUP documentation for details. */

nonterminal programc program;
nonterminal Classes class_list;
nonterminal class_c class;
nonterminal Features dummy_feature_list, feature_list;
nonterminal Feature feature;
nonterminal Formals dummy_formal_list, formal_list;
nonterminal Formal formal;
nonterminal Expression expr, nested_let;
nonterminal Expressions expr_list, dummy_expr2_list, expr2_list;
nonterminal Cases case_branches_list;
nonterminal Case branch;

/* Precedence declarations go here. */
precedence right ASSIGN;
precedence left NOT;
precedence nonassoc LT,LE, EQ;
precedence left PLUS, MINUS;
precedence left MULT, DIV;
precedence left ISVOID;
precedence left NEG;
precedence left AT;
precedence left DOT;

program	
	::= class_list:cl
	    {: RESULT = new programc(curr_lineno(), cl); :}
        ;

class_list
	/* single class */
	::= class:c
	    {: RESULT = (new Classes(curr_lineno())).appendElement(c); :}
	/* several classes */
	| class_list:cl class:c
	    {: RESULT = cl.appendElement(c); :}
	;

/* If no parent is specified, the class inherits from the Object class */
class
	::= CLASS TYPEID:n LBRACE dummy_feature_list:f RBRACE SEMI
	    {: RESULT = new class_c(curr_lineno(), n, 
		                   AbstractTable.idtable.addString("Object"), 
				   f, curr_filename()); :}
	| CLASS TYPEID:n INHERITS TYPEID:p LBRACE dummy_feature_list:f RBRACE SEMI
	    {: RESULT = new class_c(curr_lineno(), n, p, f, curr_filename()); :}
	| CLASS TYPEID:n LBRACE feature_list:f RBRACE SEMI
	    {: RESULT = new class_c(curr_lineno(), n, 
		                   AbstractTable.idtable.addString("Object"), 
				   f, curr_filename()); :}
	| CLASS TYPEID:n INHERITS TYPEID:p LBRACE feature_list:f RBRACE SEMI
	    {: RESULT = new class_c(curr_lineno(), n, p, f, curr_filename()); :}
	;

/* Feature list may be empty, but no empty features in list. */
dummy_feature_list
	::= /* empty */
	    {: RESULT = new Features(curr_lineno()); :}
	;

feature_list
	/* single feature */
	::= feature:f
	    {: RESULT = (new Features(curr_lineno())).appendElement(f); :}
	/* several features */
	| feature_list:fl feature:f
	    {: RESULT = fl.appendElement(f); :}
	;

feature
	::= OBJECTID:oid COLON TYPEID:tid SEMI
	    {: RESULT = new attr(curr_lineno(), oid, tid, new no_expr(curr_lineno())); :}
	| OBJECTID:oid LPAREN dummy_formal_list:fol  RPAREN COLON TYPEID:tid LBRACE expr:e RBRACE SEMI
	    {:RESULT = new method(curr_lineno(), oid, fol, tid, e ); :}
	| OBJECTID:oid LPAREN formal_list:fl  RPAREN COLON TYPEID:tid LBRACE expr:e RBRACE SEMI
	    {:RESULT = new method(curr_lineno(), oid, fl, tid, e ); :}
	| OBJECTID:oid COLON TYPEID:tid ASSIGN expr:e SEMI
		{: RESULT = new attr(curr_lineno(), oid, tid, e); :}
	   ;

dummy_formal_list
	::= /* empty */
	    {: RESULT = new Formals(curr_lineno()); :}
	;

formal_list
	/* single formal */
	::= formal:f
	    {: RESULT = (new Formals(curr_lineno())).appendElement(f); :}
	/* several formals */
	| formal_list:fl COMMA formal:f
	    {: RESULT = fl.appendElement(f); :}
	;

formal
	::= OBJECTID:oid COLON TYPEID:tid
		{: RESULT = new formalc(curr_lineno(), oid, tid); :}
	;

expr
	::= OBJECTID:oid ASSIGN expr:e
		{: RESULT = new assign(curr_lineno(), oid, e); :}
	| expr:e1 DOT OBJECTID:oid LPAREN dummy_expr2_list:e2l RPAREN
		{: RESULT = new dispatch(curr_lineno(), e1, oid, e2l); :}
	| expr:e1 AT TYPEID:tid DOT OBJECTID:oid LPAREN dummy_expr2_list:e2l RPAREN
		{: RESULT = new static_dispatch(curr_lineno(), e1, tid, oid, e2l); :}
	| expr:e1 AT TYPEID:tid DOT OBJECTID:oid LPAREN expr2_list:e2l RPAREN
		{: RESULT = new static_dispatch(curr_lineno(), e1, tid, oid, e2l); :}
	| expr:e1 DOT OBJECTID:oid LPAREN expr2_list:e2l RPAREN
		{: RESULT = new dispatch(curr_lineno(), e1, oid, e2l); :}
	| OBJECTID:oid LPAREN dummy_expr2_list:e2l RPAREN
		{: RESULT = new dispatch(curr_lineno(), new no_expr(curr_lineno()), oid, e2l); :}
	| OBJECTID:oid LPAREN expr2_list:e2l RPAREN
		{: RESULT = new dispatch(curr_lineno(), new no_expr(curr_lineno()), oid, e2l); :}
	| IF expr:ef THEN expr:et ELSE expr:es FI
		{: RESULT = new cond(curr_lineno(), ef, et, es); :}
	| WHILE expr:ew LOOP expr:eo POOL
		{: RESULT = new loop(curr_lineno(), ew, eo); :}
	| LBRACE expr_list:el RBRACE
		{: RESULT= new block(curr_lineno(), el); :}
	| LET OBJECTID:oid COLON TYPEID:tid nested_let:nl
		{: RESULT = new let(curr_lineno(), oid, tid, new no_expr(curr_lineno()), nl); :}
	| LET OBJECTID:oid COLON TYPEID:tid ASSIGN expr:e nested_let:nl
		{: RESULT = new let(curr_lineno(), oid, tid, e, nl); :}		
	| CASE expr:e OF case_branches_list:bl ESAC
		{: RESULT = new typcase(curr_lineno(), e, bl); :}
    | NEW TYPEID:tid
	    {:RESULT = new new_(curr_lineno(), tid); :}
    | ISVOID expr:e
	    {:RESULT = new isvoid(curr_lineno(), e); :}
	| expr:e1 PLUS expr:e2
	    {:RESULT = new plus(curr_lineno(), e1, e2); :} 
	| expr:e1 MINUS expr:e2
	    {:RESULT = new sub(curr_lineno(), e1, e2); :}
	| expr:e1 MULT expr:e2
	    {:RESULT = new mul(curr_lineno(), e1, e2); :}
	| expr:e1 DIV expr:e2
	    {:RESULT = new divide(curr_lineno(), e1, e2); :}						
    | NEG expr:e
	    {:RESULT = new neg(curr_lineno(), e); :}
    | expr:e1 LT expr:e2
	    {:RESULT = new lt(curr_lineno(), e1, e2);:}
    | expr:e1 LE expr:e2
	    {:RESULT = new leq(curr_lineno(), e1, e2);:}		
    | expr:e1 EQ expr:e2
	    {:RESULT = new eq(curr_lineno(), e1, e2);:}
    | NOT expr:e
	    {:RESULT = new comp(curr_lineno(), e); :}
    | LPAREN expr:e RPAREN
	    {:RESULT = e; :}
	| OBJECTID:oid
		{: RESULT = new object(curr_lineno(), oid); :}
    | INT_CONST:i
	    {: RESULT = new int_const(curr_lineno(), i); :}	
    | STR_CONST:s
	    {:RESULT = new string_const(curr_lineno(), s); :}
    | BOOL_CONST:b 
	    {:RESULT = new bool_const(curr_lineno(), b); :}	
	;

expr_list
	/* single expr */
	::= expr:e SEMI
	    {: RESULT = (new Expressions(curr_lineno())).appendElement(e); :}
	/* several exprs */
	| expr_list:el expr:e SEMI
	    {: RESULT = el.appendElement(e); :}
	;

dummy_expr2_list
	::= /* empty */
	    {: RESULT = new Expressions(curr_lineno()); :}
	;

expr2_list
	/* single formal */
	::= expr:e
	    {: RESULT = (new Expressions(curr_lineno())).appendElement(e); :}
	/* several formals */
	| expr2_list:el COMMA expr:e
	    {: RESULT = el.appendElement(e); :}
	;

case_branches_list
	/* single branch */
	::= branch:b
	    {: RESULT = (new Cases(curr_lineno())).appendElement(b); :}
	/* several branches */
	| case_branches_list:bl branch:b
	    {: RESULT = bl.appendElement(b); :}
	;

branch
	::= OBJECTID:oid COLON TYPEID:tid DARROW expr:e SEMI
		{: RESULT = new branch(curr_lineno(), oid, tid, e); :}
	;

nested_let
	::= IN expr:e
		{: RESULT = e; :}
	| COMMA OBJECTID:oid COLON TYPEID:tid nested_let:nl
		{: RESULT = new let(curr_lineno(), oid, tid, new no_expr(curr_lineno()), nl); :}
	| COMMA OBJECTID:oid COLON TYPEID:tid  ASSIGN expr:e nested_let:nl
		{: RESULT = new let(curr_lineno(), oid, tid, e, nl); :}		
	;